buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

ext.configFile = file "build.properties"

configFile.withReader {
	def prop = new Properties()
	prop.load(it)
	ext.config = new ConfigSlurper().parse prop
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = "${config.version.minecraft}-${config.version.mod}"
group = "minechem"
archivesBaseName = "Minechem"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
    version = "${config.version.minecraft}-${config.version.forge}"
    runDir = "run"
 
    mappings = "${config.version.mappings}"
    
	// useDepAts = true
	// makeObfSourceJar = false
	
	replaceIn "Compendium.java"
	replace "@MODVERSION@", project.version
	
	replaceIn "package-info.java"
	replace "@APIVERSION@", project.version
}

repositories {
}

dependencies {
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    manifest {
        attributes 'FMLCorePlugin': 'minechem.common.asm.LoadingPlugin'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
    }
}

task devJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

artifacts {
    archives devJar
}
